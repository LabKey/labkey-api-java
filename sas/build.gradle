import org.labkey.gradle.util.BuildUtils
import org.labkey.gradle.util.GroupNames
import org.labkey.gradle.plugin.extension.TeamCityExtension

// We add the TeamCity extension here if it doesn't exist because we will use the build
// number property from TeamCity in the distribution artifact names, if present.
TeamCityExtension teamCityExt  = project.getExtensions().findByType(TeamCityExtension.class)
if (TeamCityExtension.isOnTeamCity(project) && teamCityExt == null)
    project.extensions.create("teamCity", TeamCityExtension, project)

configurations {
    remoteApi
}

dependencies {
    BuildUtils.addLabKeyDependency(project: project, config: "remoteApi", depProjectPath: BuildUtils.getRemoteApiProjectPath(gradle), depVersion: project.labkeyClientApiVersion)
}


project.task("distribution",
        group: GroupNames.DISTRIBUTION,
        description: "Create a zip file for the SAS API client distribution containing the SAS macros, java client api jar and its dependencies",
        type: Zip,
        {Zip zip ->
            zip.archiveBaseName.set("LabKey${BuildUtils.getDistributionVersion(project)}-ClientAPI-SAS")
            zip.from (project.file("macros"))
            zip.from project.configurations.remoteApi
            zip.into zip.archiveBaseName
            zip.destinationDirectory = project.rootProject.file("dist/client-api/sas")
        }
)
