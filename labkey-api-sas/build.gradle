import org.labkey.gradle.util.BuildUtils
import org.labkey.gradle.util.GroupNames
import org.labkey.gradle.util.PomFileHelper
import org.labkey.gradle.plugin.extension.TeamCityExtension

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }
        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url "${artifactory_contextUrl}/libs-release"

        if (hasProperty('artifactory_user') && hasProperty('artifactory_password'))
        {
            credentials {
                username = artifactory_user
                password = artifactory_password
            }
            authentication {
                basic(BasicAuthentication)
                // enable preemptive authentication to get around https://www.jfrog.com/jira/browse/RTFACT-4434
            }
        }
    }
}

group 'org.labkey.api'

version '1.0.1'

// We add the TeamCity extension here if it doesn't exist because we will use the build
// number property from TeamCity in the distribution artifact names, if present.
TeamCityExtension teamCityExt  = project.getExtensions().findByType(TeamCityExtension.class)
if (TeamCityExtension.isOnTeamCity(project) && teamCityExt == null)
    project.extensions.create("teamCity", TeamCityExtension, project)

configurations {
    remoteApi
}

BuildUtils.addLabKeyDependency(project: project, config: "remoteApi", depProjectPath: BuildUtils.getRemoteApiProjectPath(gradle), depVersion: project.labkeyClientApiVersion)

project.task("distribution",
        group: GroupNames.DISTRIBUTION,
        description: "Create a zip file for the SAS API client distribution containing the SAS macros, java client api jar and its dependencies",
        type: Zip,
        {Zip zip ->
            zip.from (project.file("macros"))
            zip.from project.configurations.remoteApi
            zip.into zip.archiveBaseName
            zip.destinationDirectory = project.rootProject.file("dist/client-api/sas")
        }
)

artifacts {
    archives project.tasks.distribution
}

if (hasProperty('artifactory_user') && hasProperty('artifactory_password'))
{
    project.publishing {
        publications {
            libs(MavenPublication) {
                groupId = project.group
                artifact project.tasks.distribution
                pom {
                    name = "LabKey Server SAS Client API"
                    description = "Library of macros for use in accessing LabKey Server via SAS"
                    url = PomFileHelper.LABKEY_ORG_URL
                    developers PomFileHelper.getLabKeyTeamDevelopers()
                    licenses PomFileHelper.getApacheLicense()
                    organization PomFileHelper.getLabKeyOrganization()
                    scm {
                        connection = 'scm:git:https://github.com/LabKey/labkey-api-java/sas'
                        developerConnection = 'scm:git:https://github.com/LabKey/labkey-api-java/sas'
                        url = 'scm:git:https://github.com/LabKey/labkey-api-java/sas'
                    }
                }
            }
        }
        repositories {
            maven {
                url project.version.contains("SNAPSHOT") ? "${artifactory_contextUrl}/libs-snapshot-local" : "${artifactory_contextUrl}/libs-release-local"
                credentials {
                    username = artifactory_user
                    password = artifactory_password
                }
                authentication {
                    basic(BasicAuthentication)
                    // enable preemptive authentication to get around https://www.jfrog.com/jira/browse/RTFACT-4434
                }
            }
        }
    }

    project.model {
        tasks.publishLibsPublicationToMavenLocal {
            enabled = false
        }
    }
}
